From oadt Require Import lang_oadt.base.
From oadt Require Import lang_oadt.syntax.

Import syntax.notations.

Implicit Types (b : bool).

(** * Definitions *)

(** ** OADT value typing *)
(** This corresponds to the auxiliary oblivious value typing relation in Fig. 10
in the paper. *)
(** [ovalty v Ï‰] means [v] is an oblivious value of oblivious type value [Ï‰].
This is essentially a subset of [typing], but we have it so that the dynamic
semantics does not depend on typing. *)
Inductive ovalty : expr -> expr -> Prop :=
| OTUnit : ovalty <{ () }> <{ ðŸ™ }>
| OTOBool b : ovalty <{ [b] }> <{ ~ð”¹ }>
| OTProd v1 v2 Ï‰1 Ï‰2 :
    ovalty v1 Ï‰1 -> ovalty v2 Ï‰2 ->
    ovalty <{ (v1, v2) }> <{ Ï‰1 * Ï‰2 }>
| OTOSum b v Ï‰1 Ï‰2 :
    ovalty v <{ ite b Ï‰1 Ï‰2 }> ->
    (* Make sure the unused oblivious type is a value. *)
    otval <{ ite b Ï‰2 Ï‰1 }> ->
    ovalty <{ [inj@b<Ï‰1 ~+ Ï‰2> v] }> <{ Ï‰1 ~+ Ï‰2 }>
.

(** ** Evaluation context (â„‡) *)
(* This style is inspired by Iron Lambda. Maybe I should try other encoding
style later. This one can be quite annoying for proof automation. *)
(** This corresponds to â„‡ in Fig. 10 in the paper. *)
(** We define evaluation context [â„‡] as the hole-filling function. [â„‡ e] fills
the hole in [â„‡] with [e]. [ectx â„‡] asserts that [â„‡] is a well-formed
context.

NOTE: we reduce applications from right to left for some subtle reason. *)
Inductive ectx : (expr -> expr) -> Prop :=
| CtxProd1 Ï„2 : ectx (fun Ï„1 => <{ Ï„1 * Ï„2 }>)
| CtxProd2 Ï‰1 : otval Ï‰1 -> ectx (fun Ï„2 => <{ Ï‰1 * Ï„2 }>)
| CtxOSum1 Ï„2 : ectx (fun Ï„1 => <{ Ï„1 ~+ Ï„2 }>)
| CtxOSum2 Ï‰1 : otval Ï‰1 -> ectx (fun Ï„2 => <{ Ï‰1 ~+ Ï„2 }>)
| CtxApp1 e1 : ectx (fun e2 => <{ e1 e2 }>)
| CtxApp2 v2 : val v2 -> ectx (fun e1 => <{ e1 v2 }>)
| CtxLet e2 : ectx (fun e1 => <{ let e1 in e2 }>)
| CtxSec : ectx (fun e => <{ sð”¹ e }>)
| CtxIte e1 e2 : ectx (fun e0 => <{ if e0 then e1 else e2 }>)
| CtxMux1 e1 e2 : ectx (fun e0 => <{ ~if e0 then e1 else e2 }>)
| CtxMux2 v0 e2 : val v0 -> ectx (fun e1 => <{ ~if v0 then e1 else e2 }>)
| CtxMux3 v0 v1 : val v0 -> val v1 -> ectx (fun e2 => <{ ~if v0 then v1 else e2 }>)
| CtxPair1 e2 : ectx (fun e1 => <{ (e1, e2) }>)
| CtxPair2 v1 : val v1 -> ectx (fun e2 => <{ (v1, e2) }>)
| CtxProj b : ectx (fun e => <{ Ï€@b e }>)
| CtxInj b Ï„ : ectx (fun e => <{ inj@b<Ï„> e }>)
| CtxOInj1 b e : ectx (fun Ï„ => <{ ~inj@b<Ï„> e }>)
| CtxOInj2 b Ï‰ : otval Ï‰ -> ectx (fun e => <{ ~inj@b<Ï‰> e }>)
| CtxCase l e1 e2: ectx (fun e0 => <{ case{l} e0 of e1 | e2 }>)
| CtxFold X : ectx (fun e => <{ fold<X> e }>)
| CtxUnfold X : ectx (fun e => <{ unfold<X> e }>)
.

(** ** Small-step relation *)
Section step.

Context (Î£ : gctx).

Reserved Notation "e '-->!' e'" (at level 40).

(** This corresponds to the step relation in Fig. 10 in the paper. *)
Inductive step : expr -> expr -> Prop :=
| SApp Ï„ e v :
    val v ->
    <{ (\:Ï„ => e) v }> -->! <{ e^v }>
| SLet v e :
    val v ->
    <{ let v in e }> -->! <{ e^v }>
| SIf b e1 e2 :
    <{ if b then e1 else e2 }> -->! <{ ite b e1 e2 }>
| SCase b Ï„ v e1 e2 :
    val v ->
    <{ case inj@b<Ï„> v of e1 | e2 }> -->! <{ ite b (e1^v) (e2^v) }>
| SProj b v1 v2 :
    val v1 -> val v2 ->
    <{ Ï€@b (v1, v2) }> -->! <{ ite b v1 v2 }>
| SUnfold X X' v :
    val v ->
    <{ unfold<X> (fold <X'> v) }> -->! v
| SFun x Ï„ e :
    Î£ !! x = Some (DFun Ï„ e) ->
    <{ gvar x }> -->! <{ e }>
| SOADT X Ï„ e v :
    val v ->
    Î£ !! X = Some (DOADT Ï„ e) ->
    <{ (gvar X) v }> -->! <{ e^v }>
| SSec b :
    <{ sð”¹ b }> -->! <{ [b] }>
| SOInj b Ï‰ v :
    otval Ï‰ -> oval v ->
    <{ ~inj@b<Ï‰> v }> -->! <{ [inj@b<Ï‰> v] }>
| SMux b v1 v2 :
    val v1 -> val v2 ->
    <{ ~if [b] then v1 else v2 }> -->! <{ ite b v1 v2 }>
(* One of the most interesting rules. *)
| SOCase b Ï‰1 Ï‰2 v e1 e2 v1 v2 :
    oval v ->
    ovalty v1 Ï‰1 -> ovalty v2 Ï‰2 ->
    <{ ~case [inj@b<Ï‰1 ~+ Ï‰2> v] of e1 | e2 }> -->!
      <{ ~if [b] then (ite b (e1^v) (e1^v1)) else (ite b (e2^v2) (e2^v)) }>
(* Step under evaluation context *)
| SCtx â„‡ e e' :
    ectx â„‡ ->
    e -->! e' ->
    â„‡ e -->! â„‡ e'

where "e '-->!' e'" := (step e e').

End step.

(** * Notations *)
Module notations.

Notation "Î£ 'âŠ¨' e '-->!' e'" := (step Î£ e e') (at level 40,
                                                e custom oadt at level 0,
                                                e' custom oadt at level 0).

Notation "Î£ 'âŠ¨' e '-->*' e'" := (rtc (step Î£) e e')
                                  (at level 40,
                                   e custom oadt at level 0,
                                   e' custom oadt at level 0).

Notation "Î£ 'âŠ¨' e '-->{' n '}' e'" := (nsteps (step Î£) n e e')
                                        (at level 40,
                                         e custom oadt at level 0,
                                         n constr at level 0,
                                         e' custom oadt at level 0,
                                         format "Î£  'âŠ¨'  e  '-->{' n '}'  e'").

End notations.
