From oadt.lang_oadt Require Import base.

Implicit Types (b : bool) (x X y Y : atom) (L : aset).

Open Scope type_scope.

(** * Definitions *)

(** ** Obliviousness label *)
(** Defined as alias of [bool]. An oblivious construct (with [LObliv] label) may
accept oblivious components, and a public construct (with [LPub] label) only
accepts public components. *)
Notation olabel := bool (only parsing).
Notation LObliv := true (only parsing).
Notation LPub := false (only parsing).

(** ** Expressions (e, œÑ) *)
(** We use locally nameless representation for binders. *)
Inductive expr :=
(* Locally bound variables, i.e. de Bruijn indices. *)
| EBVar (k : nat)
(* Free variables *)
| EFVar (x : atom)
(* Global variables, referring to global functions, ADTs and OADTs. *)
| EGVar (x : atom)
(* Functions *)
| EPi (œÑ1 œÑ2: expr)
| EAbs (œÑ e : expr)
| EApp (e1 e2 : expr)
(* Oblivious type application *)
| ETApp (X : atom) (e : expr)
(* Let binding *)
| ELet (e1 e2 : expr)
(* Unit *)
| EUnitT
| EUnitV
(* Public and oblivious Booleans *)
| EBool (l : olabel)
| ELit (b : bool)
(* Boolean section *)
| ESec (e : expr)
(* Public conditional. *)
| EIte (e0 e1 e2 : expr)
(* Public and oblivious product, pair and projection. Technically we do not need
to distinguish bewteen public and oblivious product, but this is conceptually
cleaner in my opinion. It also makes certain aspects of semantics and type
checking more convenient. Note that the oblivious label can be easily inferred
and elaborated, so the surface language needs not distinguish them. *)
| EProd (l : olabel) (œÑ1 œÑ2 : expr)
| EPair (l : olabel) (e1 e2 : expr)
| EProj (l : olabel) (b : bool) (e : expr)
(* Public and oblivious sum, injection and case analysis. *)
| ESum (l : olabel) (œÑ1 œÑ2 : expr)
| EInj (l : olabel) (b : bool) (œÑ e : expr)
| ECase (l : olabel) (e0 : expr) (e1 : expr) (e2 : expr)
(* Introduction and elimination of ADTs *)
| EFold (X : atom) (e : expr)
| EUnfold (X : atom) (e : expr)
(* Oblivious conditional, i.e. MUX. *)
| EMux (e0 e1 e2 : expr)
(* Runtime expressions *)
| EBoxedLit (b : bool)
| EBoxedInj (b : bool) (œÑ e : expr)
.

(** [expr] has decidable equality. *)
#[global]
Instance expr_eq : EqDecision expr.
Proof.
  solve_decision.
Defined.

(** ** Global definitions (D) *)
Variant gdef :=
| DADT (e : expr)
| DOADT (œÑ e : expr)
| DFun (t : expr) (e : expr)
.

(** ** Global context (Œ£) *)
(** A store of global definitions. *)
Definition gctx := amap gdef.

(** This type class trick allows us to omit the global context parameter when it
is available as section variables. *)
Existing Class gctx.
#[export]
Hint Extern 1 (gctx) => assumption : typeclass_instances.
#[export]
Hint Unfold gctx : typeclass_instances.

(** ** Programs (P) *)
Notation program := (gctx * expr).


(** * Notations for expressions *)
Module expr_notations.

(* Adapted from _Software Foundations_. *)
Coercion ELit : bool >-> expr.

(** Quote *)
Notation "<{ e }>" := e (e custom oadt at level 99).
(** Lispy unquote *)
Notation "',(' e ')'" := e (in custom oadt at level 0,
                               e constr at level 0).

Notation "( x )" := x (in custom oadt, x at level 99).
Notation "x" := x (in custom oadt at level 0, x constr at level 0).
Notation "'bvar' x" := (EBVar x) (in custom oadt at level 0, x constr at level 0).
Notation "'fvar' x" := (EFVar x) (in custom oadt at level 0, x constr at level 0,
                             only parsing).
Notation "'gvar' x" := (EGVar x) (in custom oadt at level 0, x constr at level 0).
Notation "'lit' b" := (ELit b) (in custom oadt at level 0, b constr at level 0,
                            only parsing).
Notation "'ùüô'" := EUnitT (in custom oadt at level 0).
Notation "'Unit'" := EUnitT (in custom oadt at level 0, only parsing).
Notation "'ùîπ{' l '}'" := (EBool l) (in custom oadt at level 0,
                               l constr at level 0,
                               format "'ùîπ{' l '}'").
Notation "'ùîπ'" := (EBool LPub) (in custom oadt at level 0).
Notation "'Bool'" := (EBool LPub) (in custom oadt at level 0, only parsing).
Notation "'~ùîπ'" := (EBool LObliv) (in custom oadt at level 0).
Notation "'~Bool'" := (EBool LObliv) (in custom oadt at level 0, only parsing).
Notation "œÑ1 '*{' l '}' œÑ2" := (EProd l œÑ1 œÑ2) (in custom oadt at level 3,
                                     l constr at level 0,
                                     œÑ1 custom oadt,
                                     œÑ2 custom oadt at level 0,
                                     format "œÑ1  '*{' l '}'  œÑ2").
Notation "œÑ1 * œÑ2" := (EProd LPub œÑ1 œÑ2) (in custom oadt at level 3,
                            œÑ1 custom oadt,
                            œÑ2 custom oadt at level 0).
Notation "œÑ1 ~* œÑ2" := (EProd LObliv œÑ1 œÑ2) (in custom oadt at level 3,
                             œÑ1 custom oadt,
                             œÑ2 custom oadt at level 0).
Notation "œÑ1 '+{' l '}' œÑ2" := (ESum l œÑ1 œÑ2) (in custom oadt at level 4,
                                     l constr at level 0,
                                     left associativity,
                                     format "œÑ1  '+{' l '}'  œÑ2").
Notation "œÑ1 + œÑ2" := (ESum LPub œÑ1 œÑ2) (in custom oadt at level 4,
                            left associativity).
Notation "œÑ1 ~+ œÑ2" := (ESum LObliv œÑ1 œÑ2) (in custom oadt at level 4,
                             left associativity).
Notation "'Œ†' : œÑ1 , œÑ2" := (EPi œÑ1 œÑ2)
                              (in custom oadt at level 50,
                                  right associativity,
                                  format "Œ† : œÑ1 ,  œÑ2").
Notation "\ : œÑ '=>' e" := (EAbs œÑ e)
                             (in custom oadt at level 90,
                                 œÑ custom oadt at level 99,
                                 e custom oadt at level 99,
                                 left associativity,
                                 format "\ : œÑ  =>  e").
Notation "e1 e2" := (EApp e1 e2) (in custom oadt at level 2, left associativity).
Notation "X @ e" := (ETApp X e) (in custom oadt at level 2,
                          format "X @ e").
Notation "()" := EUnitV (in custom oadt at level 0).
Notation "( x , y , .. , z ){ l }" := (EPair l .. (EPair l x y) .. z)
                                        (in custom oadt at level 0,
                                            l constr at level 0,
                                            x custom oadt at level 99,
                                            y custom oadt at level 99,
                                            z custom oadt at level 99,
                                            format "( x ,  y ,  .. ,  z ){ l }").
Notation "( x , y , .. , z )" := (EPair LPub .. (EPair LPub x y) .. z)
                                   (in custom oadt at level 0,
                                       x custom oadt at level 99,
                                       y custom oadt at level 99,
                                       z custom oadt at level 99).
Notation "~( x , y , .. , z )" := (EPair LObliv .. (EPair LObliv x y) .. z)
                                    (in custom oadt at level 0,
                                        x custom oadt at level 99,
                                        y custom oadt at level 99,
                                        z custom oadt at level 99,
                                        format "~( x ,  y ,  .. ,  z )").
Notation "'œÄ{' l '}@' b e" := (EProj l b e) (in custom oadt at level 2,
                                    l constr at level 0,
                                    b constr at level 0,
                                    format "œÄ{ l }@ b  e").
Notation "'œÄ@' b e" := (EProj LPub b e) (in custom oadt at level 2,
                             b constr at level 0,
                             format "œÄ@ b  e").
Notation "'~œÄ@' b e" := (EProj LObliv b e) (in custom oadt at level 2,
                              b constr at level 0,
                              format "~œÄ@ b  e").
Notation "'œÄ1' e" := (EProj LPub true e) (in custom oadt at level 2).
Notation "'œÄ2' e" := (EProj LPub false e) (in custom oadt at level 2).
Notation "'~œÄ1' e" := (EProj LObliv true e) (in custom oadt at level 2).
Notation "'~œÄ2' e" := (EProj LObliv false e) (in custom oadt at level 2).
Notation "'sùîπ' e" := (ESec e) (in custom oadt at level 2).
Notation "'if' e0 'then' e1 'else' e2" := (EIte e0 e1 e2)
                                            (in custom oadt at level 89,
                                                e0 custom oadt at level 99,
                                                e1 custom oadt at level 99,
                                                e2 custom oadt at level 99,
                                                left associativity).
Notation "'let' e1 'in' e2" := (ELet e1 e2)
                                 (in custom oadt at level 0,
                                     e1 custom oadt at level 99,
                                     e2 custom oadt at level 99).
Notation "'inj{' l '}@' b < œÑ > e" := (EInj l b œÑ e) (in custom oadt at level 2,
                                            l constr at level 0,
                                            b constr at level 0,
                                            œÑ custom oadt at level 0,
                                            format "'inj{' l '}@' b < œÑ >  e").
Notation "'inl{' l '}' < œÑ > e" := (EInj l true œÑ e) (in custom oadt at level 2,
                                         œÑ custom oadt at level 0,
                                         format "inl{ l } < œÑ >  e").
Notation "'inr{' l '}' < œÑ > e" := (EInj l false œÑ e) (in custom oadt at level 2,
                                         œÑ custom oadt at level 0,
                                         format "inr{ l } < œÑ >  e").
Notation "'inj@' b < œÑ > e" := (EInj LPub b œÑ e) (in custom oadt at level 2,
                                     b constr at level 0,
                                     œÑ custom oadt at level 0,
                                     format "inj@ b < œÑ >  e").
Notation "'inl' < œÑ > e" := (EInj LPub true œÑ e) (in custom oadt at level 2,
                                  œÑ custom oadt at level 0,
                                  format "inl < œÑ >  e").
Notation "'inr' < œÑ > e" := (EInj LPub false œÑ e) (in custom oadt at level 2,
                                  œÑ custom oadt at level 0,
                                  format "inr < œÑ >  e").
Notation "'~inj@' b < œÑ > e" := (EInj LObliv b œÑ e) (in custom oadt at level 2,
                                      b constr at level 0,
                                      œÑ custom oadt at level 0,
                                      format "~inj@ b < œÑ >  e").
Notation "'~inl' < œÑ > e" := (EInj LObliv true œÑ e) (in custom oadt at level 2,
                                   œÑ custom oadt at level 0,
                                   format "~inl < œÑ >  e").
Notation "'~inr' < œÑ > e" := (EInj LObliv false œÑ e) (in custom oadt at level 2,
                                   œÑ custom oadt at level 0,
                                   format "~inr < œÑ >  e").
Notation "'case{' l '}' e0 'of' e1 '|' e2" :=
  (ECase l e0 e1 e2) (in custom oadt at level 89,
        l constr at level 0,
        e0 custom oadt at level 99,
        e1 custom oadt at level 99,
        e2 custom oadt at level 99,
        left associativity,
        format "'case{' l '}'  e0  'of'  e1  '|'  e2").
Notation "'case' e0 'of' e1 '|' e2" :=
  (ECase LPub e0 e1 e2) (in custom oadt at level 89,
        e0 custom oadt at level 99,
        e1 custom oadt at level 99,
        e2 custom oadt at level 99,
        left associativity).
Notation "'~case' e0 'of' e1 '|' e2" :=
  (ECase LObliv e0 e1 e2) (in custom oadt at level 89,
        e0 custom oadt at level 99,
        e1 custom oadt at level 99,
        e2 custom oadt at level 99,
        left associativity).
Notation "'fold' < X > e" := (EFold X e) (in custom oadt at level 2,
                                   X custom oadt at level 0,
                                   format "fold < X >  e").
Notation "'unfold' < X > e" := (EUnfold X e) (in custom oadt at level 2,
                                     X custom oadt at level 0,
                                     format "unfold < X >  e").
Notation "'mux' e0 e1 e2" := (EMux e0 e1 e2) (in custom oadt at level 2,
                                   e0 custom oadt at level 0,
                                   e1 custom oadt at level 0,
                                   e2 custom oadt at level 0).

Notation "[ b ]" := (EBoxedLit b) (in custom oadt at level 0,
                                      b constr at level 0).
Notation "[ 'inj@' b < œÑ > e ]" := (EBoxedInj b œÑ e)
                                     (in custom oadt at level 0,
                                         b constr at level 0,
                                         œÑ custom oadt at level 0,
                                         e custom oadt at level 0,
                                         format "[ inj@ b < œÑ >  e ]").
Notation "[ 'inl' < œÑ > e ]" := (EBoxedInj true œÑ e)
                                  (in custom oadt at level 0,
                                      œÑ custom oadt at level 0,
                                      e custom oadt at level 0,
                                      format "[ inl < œÑ >  e ]").
Notation "[ 'inr' < œÑ > e ]" := (EBoxedInj false œÑ e)
                                  (in custom oadt at level 0,
                                      œÑ custom oadt at level 0,
                                      e custom oadt at level 0,
                                      format "[ inr < œÑ >  e ]").

Notation "'ite' e0 e1 e2" := (if e0 then e1 else e2)
                               (in custom oadt at level 2,
                                   e0 constr at level 0,
                                   e1 custom oadt at level 0,
                                   e2 custom oadt at level 0).

End expr_notations.


(** * More Definitions *)
Section definitions.

Import expr_notations.
#[local]
Coercion EFVar : atom >-> expr.

(** ** Variable opening  *)
Reserved Notation "'{' k '~>' s '}' e" (in custom oadt at level 20, k constr).

Fixpoint open_ (k : nat) (s : expr) (e : expr) : expr :=
  match e with
  | <{ bvar n }> => if decide (k = n) then s else e
  | <{ Œ†:œÑ1, œÑ2 }> => <{ Œ†:({k~>s}œÑ1), {S k~>s}œÑ2 }>
  | <{ \:œÑ => e }> => <{ \:({k~>s}œÑ) => {S k~>s}e }>
  | <{ let e1 in e2 }> => <{ let {k~>s}e1 in {S k~>s}e2 }>
  | <{ case{l} e0 of e1 | e2 }> => <{ case{l} {k~>s}e0 of {S k~>s}e1 | {S k~>s}e2 }>
  (* Congruence rules *)
  | <{ e1 e2 }> => <{ ({k~>s}e1) ({k~>s}e2) }>
  | <{ X@e }> => <{ X@({k~>s}e) }>
  | <{ sùîπ e }> => <{ sùîπ ({k~>s}e) }>
  | <{ if e0 then e1 else e2 }> => <{ if {k~>s}e0 then {k~>s}e1 else {k~>s}e2 }>
  | <{ œÑ1 *{l} œÑ2 }> => <{ ({k~>s}œÑ1) *{l} ({k~>s}œÑ2) }>
  | <{ (e1, e2){l} }> => <{ ({k~>s}e1, {k~>s}e2){l} }>
  | <{ œÄ{l}@b e }> => <{ œÄ{l}@b ({k~>s}e) }>
  | <{ œÑ1 +{l} œÑ2 }> => <{ ({k~>s}œÑ1) +{l} ({k~>s}œÑ2) }>
  | <{ inj{l}@b<œÑ> e }> => <{ inj{l}@b<({k~>s}œÑ)> ({k~>s}e) }>
  | <{ fold<X> e }> => <{ fold<X> ({k~>s}e) }>
  | <{ unfold<X> e }> => <{ unfold<X> ({k~>s}e) }>
  | <{ mux e0 e1 e2 }> => <{ mux ({k~>s}e0) ({k~>s}e1) ({k~>s}e2) }>
  | _ => e
  end

where "'{' k '~>' s '}' e" := (open_ k s e) (in custom oadt).

Definition open s e := open_ 0 s e.
Notation "e ^ s" := (open s e) (in custom oadt at level 20).

(** ** Substitution *)
Reserved Notation "'{' x '‚Ü¶' s '}' e" (in custom oadt at level 20, x constr).

Fixpoint subst (x : atom) (s : expr) (e : expr) : expr :=
  match e with
  | <{ fvar y }> => if decide (x = y) then s else e
  (* Congruence rules *)
  | <{ Œ†:œÑ1, œÑ2 }> => <{ Œ†:({x‚Ü¶s}œÑ1), {x‚Ü¶s}œÑ2 }>
  | <{ \:œÑ => e }> => <{ \:({x‚Ü¶s}œÑ) => {x‚Ü¶s}e }>
  | <{ e1 e2 }> => <{ ({x‚Ü¶s}e1) ({x‚Ü¶s}e2) }>
  | <{ X@e }> => <{ X@({x‚Ü¶s}e) }>
  | <{ let e1 in e2 }> => <{ let {x‚Ü¶s}e1 in {x‚Ü¶s}e2 }>
  | <{ sùîπ e }> => <{ sùîπ ({x‚Ü¶s}e) }>
  | <{ if e0 then e1 else e2 }> => <{ if {x‚Ü¶s}e0 then {x‚Ü¶s}e1 else {x‚Ü¶s}e2 }>
  | <{ œÑ1 *{l} œÑ2 }> => <{ ({x‚Ü¶s}œÑ1) *{l} ({x‚Ü¶s}œÑ2) }>
  | <{ (e1, e2){l} }> => <{ ({x‚Ü¶s}e1, {x‚Ü¶s}e2){l} }>
  | <{ œÄ{l}@b e }> => <{ œÄ{l}@b ({x‚Ü¶s}e) }>
  | <{ œÑ1 +{l} œÑ2 }> => <{ ({x‚Ü¶s}œÑ1) +{l} ({x‚Ü¶s}œÑ2) }>
  | <{ inj{l}@b<œÑ> e }> => <{ inj{l}@b<({x‚Ü¶s}œÑ)> ({x‚Ü¶s}e) }>
  | <{ case{l} e0 of e1 | e2 }> => <{ case{l} {x‚Ü¶s}e0 of {x‚Ü¶s}e1 | {x‚Ü¶s}e2 }>
  | <{ fold<X> e }> => <{ fold<X> ({x‚Ü¶s}e) }>
  | <{ unfold<X> e }> => <{ unfold<X> ({x‚Ü¶s}e) }>
  | <{ mux e0 e1 e2 }> => <{ mux ({x‚Ü¶s}e0) ({x‚Ü¶s}e1) ({x‚Ü¶s}e2) }>
  | _ => e
  end

where "'{' x '‚Ü¶' s '}' e" := (subst x s e) (in custom oadt).

(** ** Oblivious type values (œâ) *)
Inductive otval : expr -> Prop :=
| OVUnitT : otval <{ ùüô }>
| OVOBool : otval <{ ~ùîπ }>
| OVProd œâ1 œâ2 : otval œâ1 -> otval œâ2 -> otval <{ œâ1 ~* œâ2 }>
| OVOSum œâ1 œâ2 : otval œâ1 -> otval œâ2 -> otval <{ œâ1 ~+ œâ2 }>
.

(** ** Oblivious values (v) *)
Inductive oval : expr -> Prop :=
| OVUnitV : oval <{ () }>
| OVBoxedLit b : oval <{ [b] }>
| OVPair v1 v2 : oval v1 -> oval v2 -> oval <{ ~(v1, v2) }>
| OVBoxedInj b œâ v : otval œâ -> oval v -> oval <{ [inj@b<œâ> v] }>
.

(** ** Values (v) *)
Inductive val : expr -> Prop :=
| VLit b : val <{ lit b }>
| VPair v1 v2 : val v1 -> val v2 -> val <{ (v1, v2) }>
| VAbs œÑ e : val <{ \:œÑ => e }>
| VInj b œÑ v : val v -> val <{ inj@b<œÑ> v }>
| VFold X v : val v -> val <{ fold<X> v }>
| VOVal v : oval v -> val v
.

(** ** Local closure and well-formedness of expressions *)
Inductive lc : expr -> Prop :=
| LCFVar x : lc <{ fvar x }>
| LCGVar x : lc <{ gvar x }>
| LCPi œÑ1 œÑ2 L :
  (forall x, x ‚àâ L -> lc <{ œÑ2^x }>) ->
  lc œÑ1 -> lc <{ Œ†:œÑ1, œÑ2 }>
| LCAbs œÑ e L :
  (forall x, x ‚àâ L -> lc <{ e^x }>) ->
  lc œÑ -> lc <{ \:œÑ => e }>
| LCLet e1 e2 L :
  (forall x, x ‚àâ L -> lc <{ e2^x }>) ->
  lc e1 -> lc <{ let e1 in e2 }>
| LCCase l e0 e1 e2 L1 L2 :
  (forall x, x ‚àâ L1 -> lc <{ e1^x }>) ->
  (forall x, x ‚àâ L2 -> lc <{ e2^x }>) ->
  lc e0 -> lc <{ case{l} e0 of e1 | e2 }>
(* Congruence rules *)
| LCUnitT : lc <{ ùüô }>
| LCUnitV : lc <{ () }>
| LCBool l : lc <{ ùîπ{l} }>
| LCApp e1 e2 : lc e1 -> lc e2 -> lc <{ e1 e2 }>
| LCTApp X e : lc e -> lc <{ X@e }>
| LCLit b : lc <{ lit b }>
| LCSec e : lc e -> lc <{ sùîπ e }>
| LCIte e0 e1 e2 : lc e0 -> lc e1 -> lc e2 -> lc <{ if e0 then e1 else e2 }>
| LCProd l œÑ1 œÑ2 : lc œÑ1 -> lc œÑ2 -> lc <{ œÑ1 *{l} œÑ2 }>
| LCPair l e1 e2 : lc e1 -> lc e2 -> lc <{ (e1, e2){l} }>
| LCProj l b e : lc e -> lc <{ œÄ{l}@b e }>
| LCSum l œÑ1 œÑ2 : lc œÑ1 -> lc œÑ2 -> lc <{ œÑ1 +{l} œÑ2 }>
| LCInj l b œÑ e : lc œÑ -> lc e -> lc <{ inj{l}@b<œÑ> e }>
| LCFold X e : lc e -> lc <{ fold<X> e }>
| LCUnfold X e : lc e -> lc <{ unfold<X> e }>
| LCMux e0 e1 e2 : lc e0 -> lc e1 -> lc e2 -> lc <{ mux e0 e1 e2 }>
| LCBoxedLit b : lc <{ [b] }>
(* Techincally this is not only locally closed, but more about
well-formedness. *)
| LCBoxedInj b œâ v : otval œâ -> oval v -> lc <{ [inj@b<œâ> v] }>
.

End definitions.

(** * Notations *)
Module notations.

Export expr_notations.

Notation "'{' k '~>' s '}' e" := (open_ k s e)
                                   (in custom oadt at level 20, k constr).
Notation "e ^ s" := (open s e) (in custom oadt at level 20).

Notation "'{' x '‚Ü¶' s '}' e" := (subst x s e)
                                  (in custom oadt at level 20, x constr).

(* This notation is supposed to be applied to a typing context. *)
Notation "{ x '‚Ü¶' s }" := (subst x s) (at level 20).

Notation "x # s" := (x ‚àâ stale s) (at level 40).

End notations.
