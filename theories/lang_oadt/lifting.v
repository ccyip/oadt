From oadt.lang_oadt Require Import
     base syntax semantics typing infrastructure
     admissible inversion values weakening preservation progress reveal.
Import syntax.notations semantics.notations typing.notations reveal.notations.

Implicit Types (b : bool) (x X y Y : atom) (L : aset).

#[local]
Coercion EFVar : atom >-> expr.

(** * Definitions *)

(** ** Section and retraction context (Î”) *)
(** Map ADT and OADT pair to section and retraction functions. *)
Notation srctx := (aamap (atom * atom)).

Definition srctx_wf (Î£ : gctx) : srctx -> Prop :=
  map_Forall (fun '(X, X') '(s, r) =>
                exists Ï„ Ï„k e se re,
                  Î£ !! X = Some (DADT Ï„) /\
                  Î£ !! X' = Some (DOADT Ï„k e) /\
                  Î£ !! s = Some (DFun (âŠ¥, <{ Î :Ï„k, Î ~:(gvar X), X'@(bvar 1) }>)
                                 se) /\
                  Î£ !! r = Some (DFun (âŠ¤, <{ Î :Ï„k, Î ~:(X'@(bvar 0)), gvar X }>)
                                 re)).

(* Destructive *)
Ltac apply_srctx_wf :=
  match goal with
  | Hwf : srctx_wf _ ?Î”, H : ?Î” !! _ = Some _ |- _ =>
    apply Hwf in H; try simp_hyp H
  end.

Section lifting.

Context (Î£ : gctx).
Context (Hwf : gctx_wf Î£).
Context (Î” : srctx).
Context (Hsrwf : srctx_wf Î£ Î”).

(** ** Generalized section *)
(** [gsec Ï„ Ï„' e e'] means [e'] of oblivious type [Ï„'] is a section of [e] which
has a public type [Ï„]. *)
Inductive gsec : expr -> expr -> expr -> expr -> Prop :=
| GSUnit e : gsec <{ ðŸ™ }> <{ ðŸ™ }> e <{ tape e }>
| GSBool e : gsec <{ ð”¹ }> <{ ~ð”¹ }> e <{ tape (sð”¹ e) }>
| GSProd Ï„1 Ï„1' Ï„2 Ï„2' e e1 e2 :
    gsec Ï„1 Ï„1' <{ Ï€1 e }> e1 ->
    gsec Ï„2 Ï„2' <{ Ï€2 e }> e2 ->
    gsec <{ Ï„1 * Ï„2 }> <{ Ï„1' * Ï„2' }> e <{ (e1, e2) }>
| GSSum Ï„1 Ï„1' Ï„2 Ï„2' e e1 e2 L1 L2 :
    (forall x, x âˆ‰ L1 -> gsec Ï„1 Ï„1' x <{ e1^x }>) ->
    (forall x, x âˆ‰ L2 -> gsec Ï„2 Ï„2' x <{ e2^x }>) ->
    gsec <{ Ï„1 + Ï„2 }> <{ Ï„1' ~+ Ï„2' }> e
         <{ tape (case e of ~inl<Ï„1' ~+ Ï„2'> e1 | ~inr<Ï„1' ~+ Ï„2'> e2) }>
(* Outsource to predefined context *)
| GSOADT X X' k s r e :
    Î” !! (X, X') = Some (s, r) ->
    gsec <{ gvar X }> <{ X'@k }> e <{ (gvar s) k e }>
.

(** ** Generalized retraction *)
(** [gret Ï„ Ï„' e' e] means [e] of public type [Ï„] is a retraction of [e'] which
has an oblivious type [Ï„']. Unlike [gsec], retraction also allow the oblivious
type [Ï„'] to contain non-oblivious components, as long as they match the
corresponding components in [Ï„]. *)
Inductive gret : expr -> expr -> expr -> expr -> Prop :=
| GRId Ï„ e : gret Ï„ Ï„ e e
| GRBool e' : gret <{ ð”¹ }> <{ ~ð”¹ }> e' <{ ~if tape e' then true else false }>
| GRProd Ï„1 Ï„1' Ï„2 Ï„2' e' e1 e2 :
    <{ Ï„1 * Ï„2 }> <> <{ Ï„1' * Ï„2' }> ->
    gret Ï„1 Ï„1' <{ Ï€1 e' }> e1 ->
    gret Ï„2 Ï„2' <{ Ï€2 e' }> e2 ->
    gret <{ Ï„1 * Ï„2 }> <{ Ï„1' * Ï„2' }> e' <{ (e1, e2) }>
| GRSum Ï„1 Ï„1' Ï„2 Ï„2' e' e1 e2 L1 L2 :
    <{ Ï„1 + Ï„2 }> <> <{ Ï„1' + Ï„2' }> ->
    (forall x, x âˆ‰ L1 -> gret Ï„1 Ï„1' x <{ e1^x }>) ->
    (forall x, x âˆ‰ L2 -> gret Ï„2 Ï„2' x <{ e2^x }>) ->
    gret <{ Ï„1 + Ï„2 }> <{ Ï„1' + Ï„2' }> e'
         <{ case e' of inl<Ï„1 + Ï„2> e1 | inr<Ï„1 + Ï„2> e2 }>
| GROSum Ï„1 Ï„1' Ï„2 Ï„2' e' e1 e2 L1 L2 :
    (forall x, x âˆ‰ L1 -> gret Ï„1 Ï„1' x <{ e1^x }>) ->
    (forall x, x âˆ‰ L2 -> gret Ï„2 Ï„2' x <{ e2^x }>) ->
    gret <{ Ï„1 + Ï„2 }> <{ Ï„1' ~+ Ï„2' }> e'
         <{ ~case tape e' of inl<Ï„1 + Ï„2> e1 | inr<Ï„1 + Ï„2> e2 }>
(* Outsource to predefined context *)
| GROADT X X' k s r e' :
    Î” !! (X, X') = Some (s, r) ->
    gret <{ gvar X }> <{ X'@k }> e' <{ (gvar r) k e' }>
.

(** ** Lifting *)

(** The core lifting relation. [lift_core Ï„ Ï„' e e'] means [e'] of oblivious
type [Ï„'] is the lifted result from [e] of public type [Ï„]. *)
Inductive lift_core : expr -> expr -> expr -> expr -> Prop :=
| LPi Ï„1 Ï„1' Ï„2 Ï„2' e e' r l' L1 L2 :
    (forall x, x âˆ‰ L1 -> gret Ï„1 Ï„1' x <{ r^x }>) ->
    (* Types in [lift_core] are assumed to be non-dependent types, so [Ï„2] and
    [Ï„2'] are locally closed. *)
    (forall x, x âˆ‰ L2 -> lift_core Ï„2 Ï„2' <{ e (r^x) }> <{ e'^x }>) ->
    (* The function argument of [e] should have a leakage label of [âŠ¤]
    ([LLeak]), because [e] is applied to retraction. *)
    lift_core <{ Î :{âŠ¤}Ï„1, Ï„2 }> <{ Î :{l'}Ï„1', Ï„2' }> e
              <{ \:{l'}Ï„1' => e' }>
| LSec Ï„ Ï„' e e' :
    gsec Ï„ Ï„' e e' ->
    lift_core Ï„ Ï„' e e'
.

(** Top-level relation that handles type index arguments and calls [lift_core]
to do the heavy lifting (pun intended). [lift Ï„s Ï„ Ï„' e e'] means [e] of type
[Ï„] is lifted to [e'] of type [Ï„'] which has type indices [Ï„s]. *)
Inductive lift : list expr -> expr -> expr -> expr -> expr -> Prop :=
| LCons Ï„s Ï„ Ï„1 Ï„2' e e' L :
    (forall x, x âˆ‰ L -> lift Ï„s Ï„ <{ Ï„2'^x }> e <{ e'^x }>) ->
    lift (Ï„1::Ï„s) Ï„ <{ Î :Ï„1, Ï„2' }> e <{ \:Ï„1 => e' }>
| LNil Ï„ Ï„' e e' : lift_core Ï„ Ï„' e e' -> lift [] Ï„ Ï„' e e'
.


(** ** Well-formedness of lifting input *)

(** [Ï„] is not a dependent type (at top level). It may still take as argument a
dependently typed higher-order function. *)
Fixpoint nodep (Ï„ : expr) : Prop :=
  match Ï„ with
  | <{ Î :{_}_, Ï„2 }> =>
    lc Ï„2 /\ nodep Ï„2
  | _ => True
  end.

(** [lift_type_wf Ï„s Ï„'] means [Ï„'] is a valid lifted oblivious type with
indices [Ï„s]. First, [Ï„s] should be the "prefix" of [Ï„'], i.e. [Ï„'] is of the
form [Î :Ï„1, Î :Ï„2, ..., Ï„] where [Ï„s] is [Ï„1, Ï„2, ...]. Second, index types in
[Ï„s] are locally closed, i.e. there is no dependent type in indices. Third, the
type with indices instantiated is non-dependent, i.e. [Ï„] should have the form
[Ï„1 -> Ï„2 -> ...]. They can of course depend on the indices though. *)
Inductive lift_type_wf : list expr -> expr -> Prop :=
| LTVNil Ï„' : nodep Ï„' -> lift_type_wf [] Ï„'
| LTVCons Ï„k Ï„s Ï„' L :
    lc Ï„k ->
    (forall x, x âˆ‰ L -> lift_type_wf Ï„s <{ Ï„'^x }>) ->
    lift_type_wf (Ï„k::Ï„s) <{ Î :Ï„k, Ï„' }>
.

Instance list_expr_stale : Stale (list expr) := foldr (fun e S => fv e âˆª S) âˆ….

(** * Theorems *)

(** ** Well-typedness of section and retraction *)

#[local]
Set Default Proof Using "Type".

Arguments open /.

Lemma gsec_well_typed Ï„ Ï„' e e' Î“ Îº Îº' :
  gsec Ï„ Ï„' e e' ->
  Î“ âŠ¢ Ï„ :: Îº ->
  Î“ âŠ¢ Ï„' :: Îº' ->
  Î“ âŠ¢ e :{âŠ¤} Ï„ ->
  Î“ âŠ¢ e' :{âŠ¥} Ï„'.
Proof using Hsrwf.
  intros H. revert Î“ Îº Îº'.
  induction H; intros;
    eauto using typing, kinding;
    kind_inv.
  (* GSProd *)
  - econstructor;
      try auto_eapply; eauto using kinding;
        solve [ relax_typing_type; [ econstructor | ]; eauto ].
  (* GSSum *)
  - econstructor; eauto using kinding.
    econstructor; eauto using kinding;
      simpl; simpl_cofin;
        match goal with
        | |- context [<{ ?Ï„1 +{_} ?Ï„2 }>] =>
          rewrite open_lc with (e := Ï„1) by eauto with lc;
            rewrite open_lc with (e := Ï„2) by eauto with lc
        end;
        econstructor; eauto using kinding, kinding_weakening_insert;
          auto_eapply;
          eauto using typing, kinding_weakening_insert with lc simpl_map.
  (* GSOADT *)
  - apply_srctx_wf. simplify_eq.
    repeat (first [ typing_intro
                  | relax_typing_type; [ econstructor | ] ];
            eauto); simpl; eauto.
    rewrite open_lc; eauto with lc.
Qed.

Lemma gret_well_typed Ï„ Ï„' e e' Î“ Îº Îº' :
  gret Ï„ Ï„' e' e ->
  Î“ âŠ¢ Ï„ :: Îº ->
  Î“ âŠ¢ Ï„' :: Îº' ->
  Î“ âŠ¢ e' :{âŠ¤} Ï„' ->
  Î“ âŠ¢ e :{âŠ¤} Ï„.
Proof using Hsrwf.
  intros H. revert Î“ Îº Îº'.
  induction H; intros;
    eauto using typing, kinding;
    kind_inv.

  (* GRProd *)
  econstructor;
    try auto_eapply; eauto using kinding;
      solve [ relax_typing_type; [ econstructor | ]; eauto ].

  (* GRSum and GROSum *)
  1-2:
    econstructor; eauto using typing, kinding;
    simpl; simpl_cofin;
      match goal with
      | |- context [<{ ?Ï„1 +{_} ?Ï„2 }>] =>
        rewrite open_lc with (e := Ï„1) by eauto with lc;
          rewrite open_lc with (e := Ï„2) by eauto with lc
      end;
      econstructor; eauto using kinding, kinding_weakening_insert;
        auto_eapply;
        try eapply TConv;
        eauto using typing, kinding_weakening_insert with lc simpl_map;
        reflexivity.

  (* GROADT *)
  apply_srctx_wf. simplify_eq.
  repeat (first [ typing_intro
                | relax_typing_type; [ econstructor | ] ];
          eauto); simpl; eauto.
Qed.


(** ** Well-typedness of lifting *)

#[local]
Set Default Proof Using "All".

Lemma lift_core_well_typed Ï„ Ï„' e e' Î“ Îº' :
  lift_core Ï„ Ï„' e e' ->
  nodep Ï„ ->
  nodep Ï„' ->
  Î“ âŠ¢ Ï„' :: Îº' ->
  Î“ âŠ¢ e :{âŠ¤} Ï„ ->
  Î“ âŠ¢ e' :{âŠ¥} Ï„'.
Proof.
  intros H. revert Î“ Îº'.
  induction H; intros;
    apply_regularity;
    eauto using gsec_well_typed.

  kind_inv. subst.
  econstructor; eauto.
  cbn in *. simp_hyps.
  simpl_cofin.
  repeat match goal with
         | H : lc ?Ï„ |- _ =>
           rewrite (open_lc Ï„) in * by apply H
         end.
  auto_eapply; eauto.
  relax_typing_type; [ econstructor | ];
    eauto using weakening_insert, open_lc_intro.
  eapply gret_well_typed; eauto using kinding_weakening_insert.
  eapply TConv; eauto using typing, kinding_weakening_insert with simpl_map.
  reflexivity.
  apply top_ub.
Qed.

Lemma lift_well_typed_ Ï„s Ï„ Ï„' e e' Î“ Îº' :
  lift Ï„s Ï„ Ï„' e e' ->
  nodep Ï„ ->
  lift_type_wf Ï„s Ï„' ->
  Î“ âŠ¢ Ï„' :: Îº' ->
  Î“ âŠ¢ e :{âŠ¤} Ï„ ->
  Î“ âŠ¢ e' :{âŠ¥} Ï„'.
Proof.
  intros H. revert Î“ Îº'.
  induction H; intros.
  - select (lift_type_wf _ _) (fun H => sinvert H).
    kind_inv. subst.
    econstructor; eauto.
    simpl_cofin.
    auto_eapply; eauto.
    eauto using weakening_insert.
  - qauto use: lift_core_well_typed inv: lift_type_wf.
Qed.

Theorem lift_well_typed Ï„s Ï„ Ï„' e e' Îº' :
  lift Ï„s Ï„ Ï„' e e' ->
  nodep Ï„ ->
  lift_type_wf Ï„s Ï„' ->
  âˆ… âŠ¢ Ï„' :: Îº' ->
  âˆ… âŠ¢ e :{âŠ¤} Ï„ ->
  âˆ… âŠ¢ e' :{âŠ¥} Ï„'.
Proof.
  eauto using lift_well_typed_.
Qed.

End lifting.
