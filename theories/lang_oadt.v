From oadt Require Import prelude.

(** The core language for oblivious algebraic data type. *)

Module oadt.

Section lang.

Context `{is_atom : Atom atom amap aset}.
Implicit Types (x X : atom) (L : aset).

Open Scope type_scope.

(** * Syntax *)

(** ** Expressions (e, œÑ) *)
Inductive expr :=
(** Variables *)
| EBVar (k : nat)
| EFVar (x : atom)
| EGVar (x : atom)
(** Expressions with binders *)
| EPi (œÑ1 œÑ2: expr)
| EAbs (œÑ e : expr)
| ELet (e1 e2 : expr)
| ECase (e0 : expr) (e1 : expr) (e2 : expr)
| EOCase (e0 : expr) (e1 : expr) (e2 : expr)
(** Types *)
| EUnitT
| EBool
| EOBool
| EProd (œÑ1 œÑ2 : expr)
| ESum (œÑ1 œÑ2 : expr)
| EOSum (œÑ1 œÑ2 : expr)
(** Other expressions *)
| EApp (e1 e2 : expr)
| EUnitV
| ELit (b : bool)
| ESec (e : expr)
| ERet (e : expr)
| EIte (e0 e1 e2 : expr)
| EMux (e0 e1 e2 : expr)
| EPair (e1 e2 : expr)
| EProj (b : bool) (e : expr)
| EInj (b : bool) (œÑ e : expr)
| EOInj (b : bool) (œÑ e : expr)
| EFold (X : atom) (e : expr)
| EUnfold (X : atom) (e : expr)
(** Runtime expressions *)
| EBoxedLit (b : bool)
| EBoxedOInj (b : bool) (œÑ e : expr)
.
Hint Constructors expr : expr.

(** ** GLobal definitions (D) *)
Variant gdef :=
| DADT (e : expr)
| DOADT (œÑ e : expr)
| DFun (œÑ e : expr)
.
Hint Constructors gdef : gdef.

(** ** Global named definitions (Ds) *)
Definition gdefs := list (atom * gdef).

(** ** Programs (P) *)
Definition program := gdefs * expr.

(** ** Global context (Œ£) *)
(** It is used in operational semantics and typing. *)
Notation gctx := (amap gdef).

(** ** Notations *)
(* TODO: allow them to be used outside of this section. *)

(* Adapted from _Software Foundations_. *)
Coercion ELit : bool >-> expr.
Coercion EBVar : nat >-> expr.
Coercion EGVar : atom >-> expr.

Declare Scope oadt_scope.
Delimit Scope oadt_scope with oadt.

Declare Custom Entry oadt.
Notation "<{ e }>" := e (e custom oadt at level 99).
Notation "( x )" := x (in custom oadt, x at level 99).
Notation "x" := x (in custom oadt at level 0, x constr at level 0).
Notation "'ùüô'" := EUnitT (in custom oadt at level 0).
Notation "'Unit'" := EUnitT (in custom oadt at level 0).
Notation "'ùîπ'" := EBool (in custom oadt at level 0).
Notation "'Bool'" := EBool (in custom oadt at level 0).
Notation "'~ùîπ'" := EOBool (in custom oadt at level 0).
Notation "'~Bool'" := EOBool (in custom oadt at level 0).
Notation "œÑ1 * œÑ2" := (EProd œÑ1 œÑ2) (in custom oadt at level 2,
                                        œÑ1 custom oadt,
                                        œÑ2 custom oadt at level 0).
Notation "œÑ1 + œÑ2" := (ESum œÑ1 œÑ2) (in custom oadt at level 3,
                                       left associativity).
Notation "œÑ1 ~+ œÑ2" := (EOSum œÑ1 œÑ2) (in custom oadt at level 3,
                                         left associativity).
Notation "'Œ†' : œÑ1 , œÑ2" := (EPi œÑ1 œÑ2) (in custom oadt at level 50,
                                            right associativity).
Notation "\ : œÑ '=>' e" := (EAbs œÑ e) (in custom oadt at level 90,
                                          œÑ custom oadt at level 99,
                                          e custom oadt at level 99,
                                          left associativity).
Notation "e1 e2" := (EApp e1 e2) (in custom oadt at level 1, left associativity).
Notation "()" := EUnitV (in custom oadt at level 0).
Notation "( x , y , .. , z )" := (EPair .. (EPair x y) .. z)
                                   (in custom oadt at level 0,
                                       x custom oadt at level 99,
                                       y custom oadt at level 99,
                                       z custom oadt at level 99).
Notation "'œÄ@' b e" := (EProj b e) (in custom oadt at level 0,
                                       b constr at level 0,
                                       e custom oadt at level 0).
Notation "'œÄ1' e" := (EProj true e) (in custom oadt at level 0,
                                        e custom oadt at level 0).
Notation "'œÄ2' e" := (EProj false e) (in custom oadt at level 0,
                                         e custom oadt at level 0).
Notation "'sùîπ' e" := (ESec e) (in custom oadt at level 0,
                                  e custom oadt at level 0).
Notation "'rùîπ' e" := (ERet e) (in custom oadt at level 0,
                                  e custom oadt at level 0).
Notation "'if' e0 'then' e1 'else' e2" := (EIte e0 e1 e2)
                                            (in custom oadt at level 89,
                                                e0 custom oadt at level 99,
                                                e1 custom oadt at level 99,
                                                e2 custom oadt at level 99,
                                                left associativity).
Notation "'mux' e0 e1 e2" := (EMux e0 e1 e2) (in custom oadt at level 0,
                                                 e0 custom oadt at level 0,
                                                 e1 custom oadt at level 0,
                                                 e2 custom oadt at level 0).
Notation "'let' e1 'in' e2" := (ELet e1 e2)
                                 (in custom oadt at level 0,
                                     e1 custom oadt at level 99,
                                     e2 custom oadt at level 99).
Notation "'inj@' b < œÑ > e" := (EInj b œÑ e) (in custom oadt at level 0,
                                                b constr at level 0,
                                                œÑ custom oadt at level 0,
                                                e custom oadt at level 0).
Notation "'inl' < œÑ > e" := (EInj true œÑ e) (in custom oadt at level 0,
                                                œÑ custom oadt at level 0,
                                                e custom oadt at level 0).
Notation "'inr' < œÑ > e" := (EInj false œÑ e) (in custom oadt at level 0,
                                                 œÑ custom oadt at level 0,
                                                 e custom oadt at level 0).
Notation "'~inj@' b < œÑ > e" := (EOInj b œÑ e) (in custom oadt at level 0,
                                                  b constr at level 0,
                                                  œÑ custom oadt at level 0,
                                                  e custom oadt at level 0).
Notation "'~inl' < œÑ > e" := (EOInj true œÑ e) (in custom oadt at level 0,
                                                  œÑ custom oadt at level 0,
                                                  e custom oadt at level 0).
Notation "'~inr' < œÑ > e" := (EOInj false œÑ e) (in custom oadt at level 0,
                                                   œÑ custom oadt at level 0,
                                                   e custom oadt at level 0).
Notation "'case' e0 'of' e1 '|' e2" :=
  (ECase e0 e1 e2) (in custom oadt at level 89,
                       e0 custom oadt at level 99,
                       e1 custom oadt at level 99,
                       e2 custom oadt at level 99,
                       left associativity).
Notation "'~case' e0 'of' e1 '|' e2" :=
  (EOCase e0 e1 e2) (in custom oadt at level 89,
                        e0 custom oadt at level 99,
                        e1 custom oadt at level 99,
                        e2 custom oadt at level 99,
                        left associativity).
Notation "'fold' < X > e" := (EFold X e) (in custom oadt at level 0,
                                             X custom oadt at level 0,
                                             e custom oadt at level 0).
Notation "'unfold' < X > e" := (EUnfold X e) (in custom oadt at level 0,
                                                 X custom oadt at level 0,
                                                 e custom oadt at level 0).
Notation "[ ~ b ]" := (EBoxedLit b) (in custom oadt at level 0,
                                      b constr at level 0).
Notation "[ '~inj@' b < œÑ > e ]" := (EBoxedOInj b œÑ e)
                                      (in custom oadt at level 0,
                                          b constr at level 0,
                                          œÑ custom oadt at level 0,
                                          e custom oadt at level 0).
Notation "[ '~inl' < œÑ > e ]" := (EBoxedOInj true œÑ e)
                                   (in custom oadt at level 0,
                                       œÑ custom oadt at level 0,
                                       e custom oadt at level 0).
Notation "[ '~inr' < œÑ > e ]" := (EBoxedOInj false œÑ e)
                                   (in custom oadt at level 0,
                                       œÑ custom oadt at level 0,
                                       e custom oadt at level 0).

Declare Custom Entry oadt_def.
Notation "'data' X := e" := (X, DADT e) (in custom oadt_def at level 0,
                                            X constr at level 0,
                                            e custom oadt at level 99).
Notation "'obliv' X ( : œÑ ) := e" := (X, DOADT œÑ e)
                                       (in custom oadt_def at level 0,
                                           X constr at level 0,
                                           œÑ custom oadt at level 99,
                                           e custom oadt at level 99).
Notation "'def' x : œÑ := e" := (x, DFun œÑ e) (in custom oadt_def at level 0,
                                                 x constr at level 0,
                                                 œÑ custom oadt at level 99,
                                                 e custom oadt at level 99).
Notation "[{ x }]" := (cons x nil)
                        (x custom oadt_def at level 99).
Notation "[{ x ; y ; .. ; z }]" := (cons x (cons y .. (cons z nil) ..))
                                     (x custom oadt_def at level 99,
                                      y custom oadt_def at level 99,
                                      z custom oadt_def at level 99).

(** * Examples *)
(* Axiom ‚Ñï : atom. *)
(* Axiom pred : atom. *)
(* Example nat_example := [{ *)
(*   data ‚Ñï := ùüô + ‚Ñï; *)
(*   def pred : Œ†:‚Ñï, ‚Ñï := \:‚Ñï => case unfold<‚Ñï> 0 of 1 | 0 *)
(* }]. *)

(** * Dynamic semantics *)

(** ** Polynomial algebraic data type (Œ±) *)
Inductive padt : expr -> Prop :=
| PUnitT : padt <{ ùüô }>
| PBool : padt <{ ùîπ }>
| PProd Œ±1 Œ±2 : padt Œ±1 -> padt Œ±2 -> padt <{ Œ±1 * Œ±2 }>
| PSum Œ±1 Œ±2 : padt Œ±1 -> padt Œ±2 -> padt <{ Œ±1 + Œ±2 }>
| PGVar (X : atom) : padt X
.
Hint Constructors padt : padt.

(** ** OADT values (œâ) *)
Inductive tval : expr -> Prop :=
| VUnitT : tval <{ ùüô }>
| VOBool : tval <{ ~ùîπ }>
| VProd œâ1 œâ2 : tval œâ1 -> tval œâ2 -> tval <{ œâ1 * œâ2 }>
| VOSum œâ1 œâ2 : tval œâ1 -> tval œâ2 -> tval <{ œâ1 ~+ œâ2 }>
.
Hint Constructors tval : tval.

(** ** Values (v) *)
Inductive val : expr -> Prop :=
| VUnitV : val <{ () }>
| VLit (b : bool) : val <{ b }>
| VPair v1 v2 : val v1 -> val v2 -> val <{ (v1, v2) }>
| VAbs œÑ e : val <{ \:œÑ => e }>
| VInj b œÑ v : val v -> val <{ inj@b<œÑ> v }>
| VFold X v : val v -> val <{ fold<X> v }>
| VBoxedLit (b : bool) : val <{ [~b] }>
| VBoxedOInj b œâ v : tval œâ -> val v -> val <{ [~inj@b<œâ> v] }>
.
Hint Constructors val : val.

(** ** Evaluation context (‚Ñá) *)
(* This style is inspired by Iron Lambda. *)
(** We define evaluation context [‚Ñá] as the hole-filling function. [‚Ñá e] fills
the hole in [‚Ñá] with [e]. [ectx ‚Ñá] asserts that [‚Ñá] is a well-formed
context. *)
Inductive ectx : (expr -> expr) -> Prop :=
(* | CtxTop : ectx (fun e => e) *)
| CtxProd1 œÑ2 : ectx (fun œÑ1 => <{ œÑ1 * œÑ2 }>)
| CtxProd2 œâ1 : tval œâ1 -> ectx (fun œÑ2 => <{ œâ1 * œÑ2 }>)
| CtxOSum1 œÑ2 : ectx (fun œÑ1 => <{ œÑ1 ~+ œÑ2 }>)
| CtxOSum2 œâ1 : tval œâ1 -> ectx (fun œÑ2 => <{ œâ1 ~+ œÑ2 }>)
| CtxApp1 e2 : ectx (fun e1 => <{ e1 e2 }>)
| CtxApp2 v1 : val v1 -> ectx (fun e2 => <{ v1 e2 }>)
(* TODO: Problematic! *)
| CtxApp3 (x1 : atom) : ectx (fun e2 => <{ x1 e2 }>)
| CtxLet e2 : ectx (fun e1 => <{ let e1 in e2 }>)
| CtxSec : ectx (fun e => <{ sùîπ e }>)
| CtxRet : ectx (fun e => <{ rùîπ e }>)
| CtxIte e1 e2 : ectx (fun e0 => <{ if e0 then e1 else e2 }>)
| CtxMux1 e1 e2 : ectx (fun e0 => <{ mux e0 e1 e2 }>)
| CtxMux2 v0 e2 : val v0 -> ectx (fun e1 => <{ mux v0 e1 e2 }>)
| CtxMux3 v0 v1 : val v0 -> val v1 -> ectx (fun e2 => <{ mux v0 v1 e2 }>)
| CtxPair1 e2 : ectx (fun e1 => <{ (e1, e2) }>)
| CtxPair2 v1 : val v1 -> ectx (fun e2 => <{ (v1, e2) }>)
| CtxProj b : ectx (fun e => <{ œÄ@b e }>)
| CtxInj b œÑ : ectx (fun e => <{ inj@b<œÑ> e }>)
| CtxCase e1 e2: ectx (fun e0 => <{ case e0 of e1 | e2 }>)
| CtxOInj1 b e : ectx (fun œÑ => <{ ~inj@b<œÑ> e }>)
| CtxOInj2 b œâ : tval œâ -> ectx (fun e => <{ ~inj@b<œâ> e }>)
| CtxOCase e1 e2: ectx (fun e0 => <{ ~case e0 of e1 | e2 }>)
| CtxFold X : ectx (fun e => <{ fold<X> e }>)
| CtxUnfold X : ectx (fun e => <{ unfold<X> e }>)
.
Hint Constructors ectx : ectx.

(** ** Variable opening  *)
Reserved Notation "'{' k '~>' s '}' e" (in custom oadt at level 20, k constr).
(* NOTE: recursively opening the types is probably not needed for [+] and [inj],
since their type arguments are always public, meaning that no bound variable is
possibly inside them. But I do it anyway for consistency, and possibly in the
future we allow oblivious types inside them. Let's see how this goes. I will
change it if it turns out to be too annoying for proofs. *)
Fixpoint open_ (k : nat) (s : expr) (e : expr) : expr :=
  match e with
  | EBVar n => if decide (k = n) then s else e
  | <{ Œ†:œÑ1, œÑ2 }> => <{ Œ†:{k~>s}œÑ1, {S k~>s}œÑ2 }>
  | <{ \:œÑ => e }> => <{ \:{k~>s}œÑ => {S k~>s}e }>
  | <{ let e1 in e2 }> => <{ let {k~>s}e1 in {S k~>s}e2 }>
  | <{ case e0 of e1 | e2 }> => <{ case {k~>s}e0 of {S k~>s}e1 | {S k~>s}e2 }>
  | <{ ~case e0 of e1 | e2 }> => <{ ~case {k~>s}e0 of {S k~>s}e1 | {S k~>s}e2 }>
  (** Congruence rules *)
  | <{ œÑ1 * œÑ2 }> => <{ ({k~>s}œÑ1) * ({k~>s}œÑ2) }>
  | <{ œÑ1 + œÑ2 }> => <{ ({k~>s}œÑ1) + ({k~>s}œÑ2) }>
  | <{ œÑ1 ~+ œÑ2 }> => <{ ({k~>s}œÑ1) ~+ ({k~>s}œÑ2) }>
  | <{ e1 e2 }> => <{ ({k~>s}e1) ({k~>s}e2) }>
  | <{ sùîπ e }> => <{ sùîπ ({k~>s}e) }>
  | <{ rùîπ e }> => <{ rùîπ ({k~>s}e) }>
  | <{ if e0 then e1 else e2 }> => <{ if {k~>s}e0 then {k~>s}e1 else {k~>s}e2 }>
  | <{ mux e0 e1 e2 }> => <{ mux ({k~>s}e0) ({k~>s}e1) ({k~>s}e2) }>
  | <{ (e1, e2) }> => <{ ({k~>s}e1, {k~>s}e2) }>
  | <{ œÄ@b e }> => <{ œÄ@b ({k~>s}e) }>
  | <{ inj@b<œÑ> e }> => <{ inj@b<({k~>s}œÑ)> ({k~>s}e) }>
  | <{ ~inj@b<œÑ> e }> => <{ ~inj@b<({k~>s}œÑ)> ({k~>s}e) }>
  | <{ fold<X> e }> => <{ fold<X> ({k~>s}e) }>
  | <{ unfold<X> e }> => <{ unfold<X> ({k~>s}e) }>
  | _ => e
  end

where "'{' k '~>' s '}' e" := (open_ k s e) (in custom oadt).

Definition open s t := open_ 0 s t.

Notation "t ^ s" := (open s t) (in custom oadt at level 20).

Reserved Notation "e '-->!' e'" (at level 40).
Inductive step {Œ£ : gctx} : expr -> expr -> Prop :=
| SCtx ‚Ñá e e' :
    ectx ‚Ñá ->
    e -->! e' ->
    ‚Ñá e -->! ‚Ñá e'
| SApp œÑ e v :
    val v ->
    <{ (\:œÑ => e) v }> -->! <{ e^v }>
| SLet v e :
    val v ->
    <{ let v in e }> -->! <{ e^v }>
| SCase b œÑ v e1 e2 :
    val v ->
    <{ case inj@b<œÑ> v of e1 | e2 }> -->! if b then <{ e1^v }> else <{ e2^v }>
(* TODO: [v1 : œâ1] and [v2 : œâ2]. *)
| SOCase b œâ1 œâ2 v e1 e2 v1 v2 :
    tval œâ1 -> tval œâ2 -> val v ->
    <{ ~case [~inj@b<œâ1 ~+ œâ2> v] of e1 | e2 }> -->!
      EMux <{ [~b] }> (if b then <{ e1^v }> else <{ e1^v1 }>)
                      (if b then <{ e2^v2 }> else <{ e2^v }>)
| SAppOADT X œÑ e v :
    Œ£ !! X = Some (DOADT œÑ e) ->
    <{ X v }> -->! <{ e^v }>
(* TODO: Problematic! *)
| SAppFun x œÑ e v :
    Œ£ !! x = Some (DFun œÑ e) ->
    <{ x v }> -->! <{ e v }>
| SOInj b œâ v :
    tval œâ -> val v ->
    <{ ~inj@b<œâ> v }> -->! <{ [~inj@b<œâ> v] }>
| SIte (b : bool) e1 e2 :
    <{ if b then e1 else e2 }> -->! if b then e1 else e2
(** If we also want runtime obliviousness (e.g., against malicious adversaries),
we can check [v1] and [v2] are oblivious values in this rule. *)
| SMux b v1 v2 :
    val v1 -> val v2 ->
    <{ mux [~b] v1 v2 }> -->! if b then v1 else v2
| SProj b v1 v2 :
    val v1 -> val v2 ->
    <{ œÄ@b (v1, v2) }> -->! if b then v1 else v2
| SFold X X' v :
    val v ->
    <{ unfold<X> (fold <X'> v) }> -->! v
| SSec (b : bool) :
    <{ sùîπ b }> -->! <{ [~b] }>
(** Delimited release [b] *)
| SRet (b : bool) :
    <{ rùîπ [~b] }> -->! b

where "e '-->!' e'" := (step e e').
Notation "Œ£ '‚ä¢' e '-->!' e'" := (@step Œ£ e e') (at level 40).
Hint Constructors step : step.


(** * Infrastructure *)

(** ** Locally closed *)
Inductive lc : expr -> Prop :=
| LCFVar x : lc (EFVar x)
| LCGVar x : lc (EGVar x)
| LCPi œÑ1 œÑ2 L :
    (forall x, x ‚àâ L -> lc <{ œÑ2^x }>) ->
    lc œÑ1 -> lc <{ Œ†:œÑ1, œÑ2 }>
| LCAbs œÑ e L :
    (forall x, x ‚àâ L -> lc <{ e^x }>) ->
    lc œÑ -> lc <{ \:œÑ => e }>
| LCLet e1 e2 L :
    (forall x, x ‚àâ L -> lc <{ e2^x }>) ->
    lc e1 -> lc <{ let e1 in e2 }>
| LCCase e0 e1 e2 L :
    (forall x, x ‚àâ L -> lc <{ e1^x }>) ->
    (forall x, x ‚àâ L -> lc <{ e2^x }>) ->
    lc e0 -> lc <{ case e0 of e1 | e2 }>
| LCOCase e0 e1 e2 L :
    (forall x, x ‚àâ L -> lc <{ e1^x }>) ->
    (forall x, x ‚àâ L -> lc <{ e2^x }>) ->
    lc e0 -> lc <{ ~case e0 of e1 | e2 }>
(** Congruence rules *)
| LCUnitT : lc <{ ùüô }>
| LCBool : lc <{ ùîπ }>
| LCOBool : lc <{ ~ùîπ }>
| LCProd œÑ1 œÑ2 : lc œÑ1 -> lc œÑ2 -> lc <{ œÑ1 * œÑ2 }>
| LCSum œÑ1 œÑ2 : lc œÑ1 -> lc œÑ2 -> lc <{ œÑ1 + œÑ2 }>
| LCOSum œÑ1 œÑ2 : lc œÑ1 -> lc œÑ2 -> lc <{ œÑ1 ~+ œÑ2 }>
| LCApp e1 e2 : lc e1 -> lc e2 -> lc <{ e1 e2 }>
| LCUnitV : lc <{ () }>
| LCLit b : lc (ELit b)
| LCSec e : lc e -> lc <{ sùîπ e }>
| LCRet e : lc e -> lc <{ rùîπ e }>
| LCIte e0 e1 e2 : lc e0 -> lc e1 -> lc e2 -> lc <{ if e0 then e1 else e2 }>
| LCMux e0 e1 e2 : lc e0 -> lc e1 -> lc e2 -> lc <{ mux e0 e1 e2 }>
| LCPair e1 e2 : lc e1 -> lc e2 -> lc <{ (e1, e2) }>
| LCProj b e : lc e -> lc <{ œÄ@b e }>
| LCInj b œÑ e : lc œÑ -> lc e -> lc <{ inj@b<œÑ> e }>
| LCOInj b œÑ e : lc œÑ -> lc e -> lc <{ ~inj@b<œÑ> e }>
| LCFold X e : lc e -> lc <{ fold<X> e }>
| LCUnfold X e : lc e -> lc <{ unfold<X> e }>
| LCBoxedLit b : lc <{ [~b] }>
| LCBoxedOInj b œÑ e : lc œÑ -> lc e -> lc <{ [~inj@b<œÑ> e] }>
.
Hint Constructors lc : lc.

(** ** Substitution (for local free variables) *)
Reserved Notation "'{' s '/' x '}' e" (in custom oadt at level 20, x constr).
Fixpoint subst (x : atom) (s : expr) (e : expr) : expr :=
  match e with
  | EFVar y => if decide (x = y) then s else e
  (** Congruence rules *)
  | <{ Œ†:œÑ1, œÑ2 }> => <{ Œ†:{s/x}œÑ1, {s/x}œÑ2 }>
  | <{ \:œÑ => e }> => <{ \:{s/x}œÑ => {s/x}e }>
  | <{ let e1 in e2 }> => <{ let {s/x}e1 in {s/x}e2 }>
  | <{ case e0 of e1 | e2 }> => <{ case {s/x}e0 of {s/x}e1 | {s/x}e2 }>
  | <{ ~case e0 of e1 | e2 }> => <{ ~case {s/x}e0 of {s/x}e1 | {s/x}e2 }>
  | <{ œÑ1 * œÑ2 }> => <{ ({s/x}œÑ1) * ({s/x}œÑ2) }>
  | <{ œÑ1 + œÑ2 }> => <{ ({s/x}œÑ1) + ({s/x}œÑ2) }>
  | <{ œÑ1 ~+ œÑ2 }> => <{ ({s/x}œÑ1) ~+ ({s/x}œÑ2) }>
  | <{ e1 e2 }> => <{ ({s/x}e1) ({s/x}e2) }>
  | <{ sùîπ e }> => <{ sùîπ ({s/x}e) }>
  | <{ rùîπ e }> => <{ rùîπ ({s/x}e) }>
  | <{ if e0 then e1 else e2 }> => <{ if {s/x}e0 then {s/x}e1 else {s/x}e2 }>
  | <{ mux e0 e1 e2 }> => <{ mux ({s/x}e0) ({s/x}e1) ({s/x}e2) }>
  | <{ (e1, e2) }> => <{ ({s/x}e1, {s/x}e2) }>
  | <{ œÄ@b e }> => <{ œÄ@b ({s/x}e) }>
  | <{ inj@b<œÑ> e }> => <{ inj@b<({s/x}œÑ)> ({s/x}e) }>
  | <{ ~inj@b<œÑ> e }> => <{ ~inj@b<({s/x}œÑ)> ({s/x}e) }>
  | <{ fold<X> e }> => <{ fold<X> ({s/x}e) }>
  | <{ unfold<X> e }> => <{ unfold<X> ({s/x}e) }>
  | _ => e
  end

where "'{' s '/' x '}' e" := (subst x s e) (in custom oadt).

End lang.

End oadt.
