From taypsi.lang_taypsi Require Import
     base syntax semantics typing infrastructure
     equivalence admissible inversion values preservation.
Import syntax.notations semantics.notations typing.notations equivalence.notations.

Implicit Types (b : bool) (x X y Y : atom) (L : aset).

#[local]
Coercion EFVar : atom >-> expr.

Section fix_gctx.

Context (Î£ : gctx).
Context (Hwf : gctx_wf Î£).

#[local]
Set Default Proof Using "Hwf".

(** * Lemmas about obliviousness *)

Lemma pared_obliv_preservation_inv Î“ Ï„ Ï„' Îº :
  Ï„ â‡› Ï„' ->
  Î“ âŠ¢ Ï„ :: Îº ->
  Î“ âŠ¢ Ï„' :: *@O ->
  Î“ âŠ¢ Ï„ :: *@O.
Proof.
  induction 1; intros; try case_label;
    kind_inv;
    simpl_cofin?;
    simplify_eq;
    try solve [ kinding_intro; eauto; set_shelve ];
    try easy.

  Unshelve.
  all : fast_set_solver!!.
Qed.

Lemma pared_equiv_obliv_preservation Î“ Ï„ Ï„' Îº :
  Ï„ â‰¡ Ï„' ->
  Î“ âŠ¢ Ï„ :: *@O ->
  Î“ âŠ¢ Ï„' :: Îº ->
  Î“ âŠ¢ Ï„' :: *@O.
Proof.
  induction 1; intros;
    eauto using pared_obliv_preservation_inv, pared_kinding_preservation.
Qed.

Lemma val_oval Î“ v Ï„ :
  Î“ âŠ¢ v : Ï„ ->
  Î“ âŠ¢ Ï„ :: *@O ->
  val v ->
  oval v.
Proof.
  induction 1; intros; try val_inv; try oval_inv;
    kind_inv; simplify_eq;
      try hauto lq: on ctrs: oval; try easy.

  (* TConv *)
  apply_regularity.
  auto_apply; eauto.
  eapply pared_equiv_obliv_preservation; eauto.
  equiv_naive_solver.
Qed.

(** * Canonical forms *)
Ltac canonical_form_solver :=
  inversion 1; intros; subst; eauto;
  try select (oval _) (fun H => sinvert H);
  type_inv;
  kind_inv;
  try simpl_whnf_equiv;
  simplify_eq;
  eauto 10.

Lemma canonical_form_unit Î“ e :
  val e ->
  Î“ âŠ¢ e : ðŸ™ ->
  e = <{ () }>.
Proof.
  canonical_form_solver.
Qed.

Lemma canonical_form_abs Î“ e Ï„2 Ï„1 :
  val e ->
  Î“ âŠ¢ e : Î :Ï„2, Ï„1 ->
  exists e' Ï„, e = <{ \:Ï„ => e' }>.
Proof.
  canonical_form_solver.
Qed.

Lemma canonical_form_bool Î“ e :
  val e ->
  Î“ âŠ¢ e : ð”¹ ->
  exists b, e = <{ b }>.
Proof.
  canonical_form_solver.
Qed.

Lemma canonical_form_obool Î“ e :
  val e ->
  Î“ âŠ¢ e : ~ð”¹ ->
  exists b, e = <{ [b] }>.
Proof.
  canonical_form_solver.
Qed.

Lemma canonical_form_prod Î“ e Ï„1 Ï„2 :
  val e ->
  Î“ âŠ¢ e : Ï„1 * Ï„2 ->
  exists v1 v2, val v1 /\ val v2 /\ e = <{ (v1, v2) }>.
Proof.
  canonical_form_solver.
Qed.

Lemma canonical_form_oprod Î“ e Ï„1 Ï„2 :
  val e ->
  Î“ âŠ¢ e : Ï„1 ~* Ï„2 ->
  exists v1 v2, oval v1 /\ oval v2 /\ e = <{ ~(v1, v2) }>.
Proof.
  canonical_form_solver.
Qed.

Lemma canonical_form_psi Î“ e X :
  val e ->
  Î“ âŠ¢ e : Î¨ X ->
  exists v1 v2, val v1 /\ oval v2 /\ e = <{ #(v1, v2) }>.
Proof.
  canonical_form_solver.
Qed.

Lemma canonical_form_sum Î“ e Ï„1 Ï„2 :
  val e ->
  Î“ âŠ¢ e : Ï„1 + Ï„2 ->
  exists b v Ï„, val v /\ e = <{ inj@b<Ï„> v }>.
Proof.
  canonical_form_solver.
Qed.

Lemma canonical_form_osum Î“ e Ï„1 Ï„2 :
  val e ->
  Î“ âŠ¢ e : Ï„1 ~+ Ï„2 ->
  exists b v Ï‰1 Ï‰2, oval v /\ otval Ï‰1 /\ otval Ï‰2 /\
               e = <{ [inj@b<Ï‰1 ~+ Ï‰2> v] }>.
Proof.
  canonical_form_solver.

  (* The cases when [e] is boxed injection. *)
  otval_inv.
  repeat esplit; auto.
Qed.

(** Though it seems we should have a condition of [X] being an (public) ADT, this
condition is not needed since it is implied by the typing judgment. *)
Lemma canonical_form_fold Î“ e X :
  val e ->
  Î“ âŠ¢ e : gvar X ->
  exists v X', val v /\ e = <{ fold<X'> v }>.
Proof.
  inversion 1; canonical_form_solver.
Qed.

End fix_gctx.

Ltac apply_canonical_form_lem Ï„ :=
  lazymatch Ï„ with
  | <{ ðŸ™ }> => canonical_form_unit
  | <{ ð”¹ }> => canonical_form_bool
  | <{ ~ð”¹ }> => canonical_form_obool
  | <{ _ * _ }> => canonical_form_prod
  | <{ _ ~* _ }> => canonical_form_oprod
  | <{ Î¨ _ }> => canonical_form_psi
  | <{ _ + _ }> => canonical_form_sum
  | <{ _ ~+ _ }> => canonical_form_osum
  | <{ Î :_, _ }> => canonical_form_abs
  | <{ gvar _ }> => canonical_form_fold
  end.

Ltac apply_canonical_form :=
  match goal with
  | H : val ?e, H' : _; _ âŠ¢ ?e : ?Ï„ |- _ =>
    let lem := apply_canonical_form_lem Ï„ in
    eapply lem in H; [ | solve [ eauto ] | solve [ eauto ] ]; try simp_hyp H
  end; subst.

Section fix_gctx.

Context (Î£ : gctx).
Context (Hwf : gctx_wf Î£).

#[local]
Set Default Proof Using "Hwf".

(** * Progress *)

Ltac ctx_solver :=
  match goal with
  | |- exists _, _ âŠ¨ _ -->! _ =>
    eexists; solve_ectx
  end.

(** The combined progress theorems for expressions and types. *)
Theorem progress_ :
  (forall Î“ e Ï„,
      Î“ âŠ¢ e : Ï„ ->
      Î“ = âˆ… ->
      val e \/ exists e', e -->! e') /\
  (forall Î“ Ï„ Îº,
     Î“ âŠ¢ Ï„ :: Îº ->
     Î“ = âˆ… ->
     Îº = <{ *@O }> ->
     otval Ï„ \/ exists Ï„', Ï„ -->! Ï„').
Proof.
  eapply typing_kinding_mutind; intros; subst;
    (* If a type is not used in the conclusion, the mutual inductive hypothesis
    for it is useless. Remove this hypothesis to avoid slowdown the
    automation. *)
    try match goal with
        | H : context [otval ?Ï„ \/ _] |- val ?e \/ _ =>
          assert_fails contains e Ï„; clear H
        end;
    simp_hyps; try case_label; simplify_map_eq; eauto;
    (* Solve the trivial evaluation context step. *)
    repeat
      match reverse goal with
      | H : otval _ \/ exists _, _ |- _ =>
          destruct H as [| [] ]; [ | solve [ right; ctx_solver ] ]
      end;
    repeat
      match reverse goal with
      | H : val _ \/ exists _, _ |- _ =>
          destruct H as [| [] ]; [ | solve [ right; ctx_solver ] ]
      end;
    try apply_canonical_form;
    try solve [ right; eauto using step, val; ctx_solver
              | left; eauto using val, oval, otval ].

  (* Oblivious injection. It steps to boxed injection. *)
  right. otval_inv.
  repeat econstructor; eauto.
  case_split; eauto using otval_well_kinded, val_oval.

  (* Oblivious case. *)
  right.
  select! (otval _) (fun H => use (ovalty_inhabited _ H)).
  eauto using step.

  (* Oblivious pair. *)
  left. eauto 10 using val, oval, val_oval.

  (* Psi pair. *)
  left. eauto using val, val_oval, kinding.

  (* Boxed injection. *)
  left. qauto use: ovalty_elim ctrs: val.

  (* Public product and sum. These case are impossible. *)
  1-2: enough (<{ *@P }> âŠ‘ <{ *@O }>) by easy; scongruence use: join_ub_r.

  (* Kinding subsumption *)
  select kind (fun Îº => destruct Îº); sintuition use: any_kind_otval.
Qed.

Theorem progress Ï„ e :
  âˆ… âŠ¢ e : Ï„ ->
  val e \/ exists e', e -->! e'.
Proof.
  hauto use: progress_.
Qed.

Theorem kinding_progress Ï„ :
  âˆ… âŠ¢ Ï„ :: *@O ->
  otval Ï„ \/ exists Ï„', Ï„ -->! Ï„'.
Proof.
  hauto use: progress_.
Qed.

End fix_gctx.
